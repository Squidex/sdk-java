import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.11.0'
    id "com.vanniktech.maven.publish" version "0.29.0"
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'com.google.code.gson:gson:2.9.1'
    api 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    api 'com.squareup.okhttp3:okhttp:4.12.0'
    api 'io.gsonfire:gson-fire:1.9.0'
    api 'io.swagger:swagger-annotations:1.6.8'
    api 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    api 'jakarta.ws.rs:jakarta.ws.rs-api:2.1.6'
    api 'org.openapitools:jackson-databind-nullable:0.2.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

spotless {
    java {
        palantirJavaFormat()
    }
}

javadoc {
    options.tags = ["http.response.details:a:Http Response Details"]

    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

mavenPublishing {
  publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)

  coordinates("io.squidex", "squidex", "2.0.0-RC1")

  pom {
    name = "Squidex SDK"
    description = "Client Library for the Squidex API"
    inceptionYear = "2020"
    url = "https://github.com/squidex/sdk-java"
    licenses {
      license {
        name = "MIT"
      }
    }
    developers {
      developer {
        id = "sebastianstehle"
        name = "Sebastian Stehle"
        url = "https://github.com/sebastianstehle/"
      }
    }
     scm {
      url = "https://github.com/squidex/squidex/"
      connection = "scm:git:git://github.com/squidex/squidex.git"
      developerConnection = "scm:git:ssh://git@github.com/squidex/squidex.git"
    }
  }

  signAllPublications()
}

